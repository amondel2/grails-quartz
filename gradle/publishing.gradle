import io.github.gradlenexus.publishplugin.InitializeNexusStagingRepository

ext.set('signing.keyId', findProperty('signing.keyId') ?: System.getenv('SIGNING_KEY'))
ext.set('signing.password', findProperty('signing.password') ?: System.getenv('SIGNING_PASSPHRASE'))

def javaComponent = components.named('java')
publishing {
    publications {
        register('grailsQuartzPlugin', MavenPublication) {
            from javaComponent.get()
            versionMapping {
                usage('java-api') { fromResolutionOf('runtimeClasspath') }
                usage('java-runtime') { fromResolutionResult() }
            }
            pom {
                name = 'Grails Quartz plugin'
                description = 'Grails plugin for Quartz scheduler integration'
                url = 'https://github.com/grails/grails-quartz'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'osscontributor'
                        name = 'Jeff Brown'
                        email = 'brownj@ociweb.com'
                    }
                    developer {
                        id = 'nebolsin'
                        name = 'Sergey Nebolsin'
                        email = 'nebolsin@gmail.com'
                    }
                    developer {
                        id = 'graemerocher'
                        name = 'Graeme Rocher'
                        email = 'graeme.rocher@gmail.com'
                    }
                    developer {
                        id = 'rvanderwerf'
                        name = 'Ryan Vanderwerf'
                        email = 'rvanderwerf@gmail.com'
                    }
                    developer {
                        id = 'kefirfromperm'
                        name = 'Vitalii Samolovskikh'
                        email = 'kefir@perm.ru'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/grails/grails-quartz.git'
                    developerConnection = 'scm:git:ssh://github.com:grails/grails-quartz.git'
                    url = 'https://github.com/grails/grails-quartz'
                }
            }
            // dependency management shouldn't be included
            pom.withXml {
                def root = it.asElement()
                root.getElementsByTagName('dependencyManagement').each { root.removeChild(it) }
            }
        }
    }

    if (isSnapshot) {
        repositories {
            maven {
                credentials {
                    username = findProperty('artifactoryPublishUsername') ?: ''
                    password = findProperty('artifactoryPublishPassword') ?: ''
                }
                url = uri('https://repo.grails.org/grails/plugins3-snapshots-local')
            }
        }
    }
}


def mavenPublication = extensions.findByType(PublishingExtension).publications.named('grailsQuartzPlugin')
tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}
afterEvaluate {
    signing {
        required = { isReleaseVersion }
        sign mavenPublication.get()
    }
}

if (isReleaseVersion) {
    nexusPublishing {
        String sonatypeUsername = findProperty('sonatypeUsername') ?: ''
        String sonatypePassword = findProperty('sonatypePassword') ?: ''
        String sonatypeStagingProfileId = findProperty('sonatypeStagingProfileId') ?: ''
        repositories {
            sonatype {
                nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
                username = sonatypeUsername
                password = sonatypePassword
                stagingProfileId = sonatypeStagingProfileId
            }
        }
    }
}


//do not generate extra load on Nexus with new staging repository if signing fails
tasks.withType(InitializeNexusStagingRepository).configureEach {
    shouldRunAfter = tasks.withType(Sign)
}